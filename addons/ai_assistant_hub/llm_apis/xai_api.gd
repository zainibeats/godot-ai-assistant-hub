@tool
class_name XaiAPI
extends LLMInterface

var _headers: PackedStringArray # set in _initialize function


func _rebuild_headers() -> void:
	_headers = ["Content-Type: application/json",
				"Authorization: Bearer %s" % _api_key,  # Include the key in the headers
	]
	
func _initialize() -> void:
	_rebuild_headers()
	llm_config_changed.connect(_rebuild_headers)


# Get model list 
func send_get_models_request(http_request: HTTPRequest) -> bool:
	#API key is in LLMInterface base class
	if _api_key.is_empty():
		AIHubPlugin.print_err("xAI API key not set. Configure the API key in the main tab and try again.")
		return false

	var error = http_request.request(_models_url, _headers, HTTPClient.METHOD_GET)
	if error != OK:
		AIHubPlugin.print_err("xAI API request failed: %s" % _models_url)
		return false
	return true


func read_models_response(body: PackedByteArray) -> Array[String]:
	var json := JSON.new()
	json.parse(body.get_string_from_utf8())
	var response := json.get_data()
	if response.has("data") and response.data is Array:
		var model_names: Array[String] = []
		for model in response.data:
			if model.has("id"):
				model_names.append(model.id)
		model_names.sort()
		return model_names
	else:
		return [INVALID_RESPONSE]


func _extract_content_from_json_string(s) -> String:
	# Older saved chats can contain JSON-encoded message objects inside content fields.
	var attempts := 0
	var txt = s
	while typeof(txt) == TYPE_STRING and txt.begins_with("{") and txt.find("\"content\"") != -1 and attempts < 3:
		var json := JSON.new()
		if json.parse(txt) == OK:
			var jmsg = json.get_data()
			if "content" in jmsg:
				txt = jmsg["content"]
				attempts += 1
			else:
				break
		else:
			break
	return str(txt)


func send_chat_request(http_request: HTTPRequest, message_list: Array) -> bool:
	if _api_key.is_empty():
		AIHubPlugin.print_err("xAI API key not set. Configure the API key in the main tab and spawn a new assistant.")
		return false

	if model.is_empty():
		AIHubPlugin.print_err("ERROR: You need to set an AI model for this assistant type.")
		return false

	var formatted_contents := []
	for i in range(message_list.size()):
		var msg = message_list[i]
		var role: String = str(msg.get("role", "user"))
		var text = msg.get("content", msg.get("text", msg))
		AIHubPlugin.print_msg(text)

		formatted_contents.append({
			"role": role,
			"content": str(text)
		})

	var body_dict := {
		"messages": formatted_contents,
		"model": model
	}
	if override_temperature:
		body_dict["temperature"] = temperature 

	var reasoning_effort := get_openai_style_reasoning_effort()
	if not reasoning_effort.is_empty():
		body_dict["reasoning_effort"] = reasoning_effort

	var body := JSON.stringify(body_dict)
	#print(_chat_url)

	var error = http_request.request(_chat_url, _headers, HTTPClient.METHOD_POST, body)
	
	if error != OK:
		AIHubPlugin.print_err("xAI API chat request failed.\nURL: %s\nRequest body: %s" % [_chat_url, body])
		return false
	return true


func read_response(body: PackedByteArray) -> String:
	var raw_body = body.get_string_from_utf8()
	#print("xAI API raw response: ", raw_body)
	
	var json := JSON.new()
	var parse_result := json.parse(body.get_string_from_utf8())
	#print("HTTP Response body: ", body.get_string_from_utf8())
	if parse_result != OK:
		AIHubPlugin.print_err("Failed to parse xAI response JSON: %s" % json.get_error_message())
		return INVALID_RESPONSE
	var response := json.get_data()
	if response == null:
		AIHubPlugin.print_err("xAI response is null after parsing.")
		return INVALID_RESPONSE
	if response.has("error"):
		#print("xAI API Error: ", JSON.stringify(response.error))
		AIHubPlugin.print_err("xAI API Error: " + str(response.error))
		return INVALID_RESPONSE
	if response.has("choices") and response.choices.size() > 0:
		if response.choices[0].has("message") and response.choices[0].message.has("content"):
			return _msg_cleaner.clean(response.choices[0].message.content)
	AIHubPlugin.print_err("Failed to parse xAI response: %s" % JSON.stringify(response))
	return INVALID_RESPONSE
